apply plugin: 'com.android.application'

android
{
    def targetVersionDefault = 30
    Boolean useAndroidX = project.properties['android.useAndroidX'].toBoolean()
    def targetVersion = project.properties['android.targetVersion'] != null ? project.properties['android.targetVersion'].toInteger() : null
    if (targetVersion == null) {
        targetVersion = targetVersionDefault
    }

    compileSdkVersion targetVersion

    sourceSets
    {
        main
        {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig
    {
        applicationId "com.forrestguice.suntimeswidget"
        resValue "string", "packageName", applicationId

        minSdkVersion 14
        targetSdkVersion targetVersion
        missingDimensionStrategy 'support_version', 'androidx'

        if (targetVersion == 28) {
            missingDimensionStrategy 'api', 'api28'
        } else if (targetVersion == 29) {
            missingDimensionStrategy 'api', 'api29'
        } else if (targetVersion == 30) {
            missingDimensionStrategy 'api', 'api30'
        } else if (targetVersion == 33) {
            missingDimensionStrategy 'api', 'api33'
        } else if (targetVersion == 34) {
            missingDimensionStrategy 'api', 'api34'
        } else {
            missingDimensionStrategy 'api', 'api28'
        }

        versionCode 126
        versionName "0.16.12"

        manifestPlaceholders = [suntimesAuthorityRoot:"suntimeswidget", suntimesPermissionRoot:"suntimes"]
        buildConfigField "String", "SUNTIMES_PERMISSION_ROOT", "\"suntimes\""
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "ar", "ca", "cs", "de", "en", "eo", "es", "eu", "fr", "hu", "it", "nb", "nl", "pl", "pt", "ru", "zh"
    }

    buildTypes
    {
        release
        {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        production {
            dimension "version"
        }
        legacy {
            dimension "version"
            targetSdkVersion 28
            missingDimensionStrategy 'support_version', 'supportlib'
            missingDimensionStrategy 'api', 'api28'
            applicationIdSuffix ".legacy"
            versionNameSuffix "-legacy"
            resValue "string", "packageName", "com.forrestguice.suntimeswidget.legacy"   // TODO: cleaner way to set this resValue without duplication
            manifestPlaceholders = [suntimesAuthorityRoot:"suntimeswidget.legacy", suntimesPermissionRoot:"suntimes"]
            buildConfigField "String", "SUNTIMES_PERMISSION_ROOT", "\"suntimes\""
        }
        nightly {
            dimension "version"
            applicationIdSuffix ".nightly"
            versionNameSuffix "-nightly"
            resValue "string", "packageName", "com.forrestguice.suntimeswidget.nightly"   // TODO: cleaner way to set this resValue without duplication
            manifestPlaceholders = [suntimesAuthorityRoot:"suntimeswidget.nightly", suntimesPermissionRoot:"suntimes.nightly"]
            buildConfigField "String", "SUNTIMES_PERMISSION_ROOT", "\"suntimes.nightly\""
            def nightlyVersionCode = new Date().format('yyyyMMddHH') as int
            versionCode nightlyVersionCode
        }
    }

    //noinspection GrDeprecatedAPIUsage
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (useAndroidX) {
            if (names.contains("legacy")) {
                setIgnore(true)

            } else if (targetVersion != null) {
                if ((names.contains("api28") && targetVersion != 28)
                        || (names.contains("api29") && targetVersion != 29) || (names.contains("api30") && targetVersion != 30)
                        || (names.contains("api33") && targetVersion != 33) || (names.contains("api34") && targetVersion != 34)) {
                    setIgnore(true)
                }
            }
        } else if (!useAndroidX && (names.contains("production") || names.contains("api29") || names.contains("api30") || names.contains("api33") || names.contains("api34"))) {
            setIgnore(true)
        } else if (!useAndroidX && (names.contains("nightly") || names.contains("api29") || names.contains("api30") || names.contains("api33") || names.contains("api34"))) {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    lintOptions
    {
        disable 'MissingTranslation','WrongRegion','ObsoleteSdkInt'
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies
{
    implementation project(path: ":util")
    implementation project(path: ":support")

    implementation group: 'net.time4j', name: 'time4j-android', version: '4.8-2021a'
    implementation project(path: ":lib-quadflask-colorpicker")    //implementation 'com.github.forrestguice:colorpicker:0.0.13post1'

    productionImplementation 'com.google.android.material:material:1.4.0'
    productionImplementation 'androidx.appcompat:appcompat:1.3.1'
    productionImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    productionImplementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
    productionImplementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'

    nightlyImplementation 'com.google.android.material:material:1.4.0'
    nightlyImplementation 'androidx.appcompat:appcompat:1.3.1'
    nightlyImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    nightlyImplementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
    nightlyImplementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'

    legacyImplementation 'com.android.support:design:28.0.0'
    legacyImplementation 'com.android.support:appcompat-v7:28.0.0'
    legacyImplementation 'com.android.support:recyclerview-v7:28.0.0'
    legacyImplementation 'com.android.support:cardview-v7:28.0.0'
    legacyImplementation 'com.android.support.constraint:constraint-layout:2.0.4'
    legacyImplementation 'android.arch.lifecycle:extensions:1.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.jraska:falcon:2.2.0'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

tasks.withType(Test) 
{
    useJUnit {
        excludeCategories 'com.forrestguice.suntimeswidget.UnlistedTest', 'com.forrestguice.suntimeswidget.FlakeyTest'
    }
    testLogging 
    {
        exceptionFormat "full"
        //events "started", "skipped", "passed", "failed"
        //showStandardStreams true
        events "failed"
        showStandardStreams false
    }
}
