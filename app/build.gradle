apply plugin: 'com.android.application'

android
{
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    sourceSets
    {
        main
        {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig
    {
        applicationId "com.forrestguice.suntimeswidget"
        //noinspection MinSdkTooLow
        minSdkVersion 10
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 26
        versionName "0.8.2"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDateAsMillis() + "L)"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes
    {
        release
        {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions
    {
        disable 'MissingTranslation'
        disable 'IconLauncherFormat'
    }
}

dependencies
{
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.github.forrestguice:sunrisesunsetlib-java:SunriseSunsetCalculator-1.2-p0-fixdst'
    compile 'com.github.caarmen.SunriseSunset:lib-sunrise-sunset:1.1.0'
    compile group: 'net.time4j', name: 'time4j-android', version: '3.40-2018b'
    compile 'com.github.QuadFlask:colorpicker:0.0.13'

    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.jraska:falcon:1.0.4'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

static def getDateAsMillis()
{
    def Calendar cal = Calendar.getInstance();
    return cal.getTimeInMillis().toString()
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
