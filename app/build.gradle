apply plugin: 'com.android.application'

android
{
    compileSdkVersion 25
    buildToolsVersion "28.0.3"

    defaultConfig
    {
        applicationId "com.forrestguice.suntimeswidget"
        //noinspection MinSdkTooLow
        minSdkVersion 10
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 42
        versionName "0.11.1"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDateAsMillis() + "L)"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes
    {
        release
        {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions
    {
        disable 'MissingTranslation', 'GoogleAppIndexingWarning'
    }
}

dependencies
{
// Android support
    implementation 'com.android.support:design:25.4.0'
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

// Other
    implementation 'com.github.forrestguice:sunrisesunsetlib-java:SunriseSunsetCalculator-1.2-p0-fixdst'
    implementation 'com.github.caarmen.SunriseSunset:lib-sunrise-sunset:1.1.0'
    //^implementation 'ca.rmen:lib-sunrise-sunset:1.1.1'
    implementation 'net.time4j:time4j-android:4.2-2018i'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'

// Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.jraska:falcon:1.0.4' //2.1.1
    androidTestImplementation 'com.android.support:support-annotations:25.4.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

configurations.all
{
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

static def getDateAsMillis()
{
    def Calendar cal = Calendar.getInstance();
    return cal.getTimeInMillis().toString()
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec
    {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
