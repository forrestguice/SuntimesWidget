plugins {
    id 'com.android.library'
}

android {
    Boolean useAndroidX = project.properties['android.useAndroidX'].toBoolean()
    def targetVersion = project.properties['android.targetVersion'] != null ? project.properties['android.targetVersion'].toInteger() : null
    if (targetVersion == null) {
        targetVersion = 28;
    }

    defaultConfig {
        if (targetVersion >= 33) {
            compileSdk 33
        }

        minSdkVersion 11
        targetSdkVersion 28
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    if (targetVersion >= 33) {
        namespace "com.forrestguice.support"
        buildFeatures {
            buildConfig = true
        }
        flavorDimensions = ["support_version", "api"]

    } else {
        //noinspection GrDeprecatedAPIUsage
        compileSdkVersion targetVersion
        //noinspection GrDeprecatedAPIUsage
        flavorDimensions "support_version", "api"
    }

    productFlavors {
        supportlib {
            dimension "support_version"
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 28
            versionNameSuffix "-supportlib"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        androidx {
            dimension "support_version"
            //noinspection GradleDependency
            if (targetVersion >= 33) {
                compileSdk 33
            }
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 30
            versionNameSuffix "-androidx"
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        }

        api28 {
            dimension "api"
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 28
            versionNameSuffix "-api28"
        }
        api30 {
            dimension "api"
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 30
            versionNameSuffix "-api30"
        }
        api33 {
            dimension "api"
            if (targetVersion >= 33) {
                compileSdk 33
            }
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 33
            versionNameSuffix "-api33"
        }
        api34 {
            dimension "api"
            if (targetVersion >= 34) {
                compileSdk 34
            }
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 34
            versionNameSuffix "-api34"
        }
    }

    configurations {
        supportlibApi28Implementation {}
        androidxApi28Implementation {}
        androidxApi30Implementation {}
        androidxApi33Implementation {}
        androidxApi34Implementation {}
    }

    if (targetVersion >= 34)
    {
        androidComponents {
            beforeVariants(selector().all(), { variant ->
                if (useAndroidX) {
                    if (variant.name.contains("supportlib")) {
                        variant.enable = false

                    } else if (targetVersion != null) {
                        if ((variant.name.contains("Api28") && targetVersion != 28) || (variant.name.contains("Api30") && targetVersion != 30)
                                || (variant.name.contains("Api33") && targetVersion != 33) || (variant.name.contains("Api34") && targetVersion != 34)) {
                            variant.enable = false
                        }
                    }
                } else if (!useAndroidX && (variant.name.contains("androidx") || variant.name.contains("Api30") || variant.name.contains("Api33") || variant.name.contains("Api34"))) {
                    variant.enable = false
                }
            })
        }
    } else {
        //noinspection GrDeprecatedAPIUsage
        variantFilter { variant ->
            def names = variant.flavors*.name
            if (useAndroidX) {
                if (names.contains("supportlib")) {
                    setIgnore(true)

                } else if (targetVersion != null) {
                    if ((names.contains("api28") && targetVersion != 28) || (names.contains("api30") && targetVersion != 30)
                            || (names.contains("api33") && targetVersion != 33) || (names.contains("api34") && targetVersion != 34)) {
                        setIgnore(true)
                    }
                }
            } else if (!useAndroidX && (names.contains("androidx") || names.contains("api30") || names.contains("api33") || names.contains("api34"))) {
                setIgnore(true)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(path: ":util")

    supportlibImplementation 'com.android.support:design:28.0.0'
    supportlibImplementation 'com.android.support:appcompat-v7:28.0.0'
    supportlibImplementation 'com.android.support.constraint:constraint-layout:2.0.4'
    supportlibImplementation 'com.android.support:recyclerview-v7:28.0.0'
    supportlibImplementation 'com.android.support:cardview-v7:28.0.0'
    supportlibImplementation 'android.arch.lifecycle:extensions:1.1.1'

    androidxApi28Implementation 'com.google.android.material:material:1.0.0'
    androidxApi28Implementation 'androidx.appcompat:appcompat:1.0.0'
    androidxApi28Implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidxApi28Implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'      // 1.3.0 (2025-02-26) requires minapi 21+
    androidxApi28Implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    androidxApi28Implementation 'androidx.lifecycle:lifecycle-livedata:1.0.0'             // 2.7.0+ requires Java 17
    androidxApi28Implementation 'androidx.lifecycle:lifecycle-viewmodel:1.0.0'            // 2.7.0+ requires Java 17

    androidxApi30Implementation 'com.google.android.material:material:1.4.0'
    androidxApi30Implementation 'androidx.appcompat:appcompat:1.3.1'
    androidxApi30Implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidxApi30Implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'      // 1.3.0 (2025-02-26) requires minapi 21+
    androidxApi30Implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    androidxApi30Implementation 'androidx.lifecycle:lifecycle-livedata:2.5.0'             // 2.7.0+ requires Java 17
    androidxApi30Implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.0'            // 2.7.0+ requires Java 17

    androidxApi33Implementation 'com.google.android.material:material:1.6.0'
    androidxApi33Implementation 'androidx.appcompat:appcompat:1.6.1'
    androidxApi33Implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidxApi33Implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'      // 1.3.0 (2025-02-26) requires minapi 21+
    androidxApi33Implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    androidxApi33Implementation 'androidx.lifecycle:lifecycle-livedata:2.5.0'             // 2.7.0+ requires Java 17
    androidxApi33Implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.0'            // 2.7.0+ requires Java 17

    androidxApi34Implementation 'com.google.android.material:material:1.6.1'
    androidxApi34Implementation 'androidx.appcompat:appcompat:1.6.1'
    androidxApi34Implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidxApi34Implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'      // 1.3.0 (2025-02-26) requires minapi 21+
    androidxApi34Implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    androidxApi34Implementation 'androidx.lifecycle:lifecycle-livedata:2.5.0'             // 2.7.0+ requires Java 17
    androidxApi34Implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.0'            // 2.7.0+ requires Java 17

    testImplementation 'junit:junit:4.12'

    Boolean useAndroidX = project.properties['android.useAndroidX'].toBoolean();
    def targetVersion = project.properties['android.targetVersion'] != null ? project.properties['android.targetVersion'].toInteger() : null

    if (useAndroidX) {
        if (targetVersion == null || targetVersion >= 33) {
            androidTestImplementation 'androidx.test.ext:junit:1.1.5'
            androidTestImplementation 'androidx.test:core:1.1.0'
            androidTestImplementation 'androidx.test:rules:1.2.0'
            androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
        } else {
            androidTestImplementation 'androidx.test.ext:junit:1.1.1'
            androidTestImplementation 'androidx.test:core:1.1.0'
            androidTestImplementation 'androidx.test:rules:1.2.0'
            androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
        }
    } else {
        androidTestImplementation 'com.android.support:support-annotations:28.0.0'
        androidTestImplementation 'com.android.support.test:runner:1.0.2'
        androidTestImplementation 'com.android.support.test:rules:0.5'
        androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
        androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    }

}